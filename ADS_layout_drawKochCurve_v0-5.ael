// Esse Script desenha o Curva de Koch
//load(fix_path("C:\Users\stefa\OneDrive\Ãrea de Trabalho\Python\AEL_scripts\ADS_layout_drawKochCurve_v0-5.ael"));


defun curve(p1, p2, layer)
{
		
	db_add_polyline(context);
	db_add_point(context, p1[0], p1[1]);
	db_add_point(context, p2[0], p2[1]);
	decl dbShape = db_end(context, layer);
	
	//de_delete(p3[0],(p2[1]-p3[1])/2);
}



defun kochCurve (p1, p2, degree){
	
	decl theta = PI/3;
	
	decl layerList = list("cond","resi","diel","diel2","hole","bond");
	decl layer = db_get_layerid(context, layerList[degree], "drawing");
	
	curve(p1, p2, layer);

	
	if(degree > 0){
		
		decl p3 = [(2*p1[0]+p2[0])/3,(2*p1[1]+p2[1])/3];
		decl p5 = [(2*p2[0]+p1[0])/3,(2*p2[1]+p1[1])/3];
 
		decl p4 = [p3[0] + (p5[0] - p3[0])*cos(theta) + (p5[1] - p3[1])*sin(theta),p3[1] - (p5[0] - p3[0])*sin(theta) + (p5[1] - p3[1])*cos(theta)];
 
		kochCurve(p1,p3,degree-1);
		kochCurve(p3,p4,degree-1);
		kochCurve(p4,p5,degree-1);
		kochCurve(p5,p2,degree-1);		
	};
}

defun clearLayers (degree){
	
	decl layerList = list("cond","resi","diel","diel2","hole","bond");
	
	
	while (degree > 0){
		decl layerId = db_get_layerid(context, layerList[degree]);
		db_select_all_on_layerid(context, layerId );
		db_delete_selected(context);
		degree--;
		};
}


defun ADS_drawKochCurve(W, H ,degree){

	de_select_all();
	de_delete();
	
	
	decl p1 = [10, H-10];
	decl p2 = [W-10, H-10];


	
	kochCurve (p1, p2, degree);
	clearLayers(degree);
	de_select_all(); 
	de_union();
}	

de_open_workspace("C:\Users\stefa\ADS_wrk\AEL_Python_wrk");

decl context = de_get_design_context_from_name("AEL_Python_lib:cell_2:layout");
de_show_context_in_new_window(context);


ADS_drawKochCurve(200, 200 ,4);


